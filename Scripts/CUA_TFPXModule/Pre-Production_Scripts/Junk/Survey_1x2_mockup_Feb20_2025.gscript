# This script is to survey a 1x2 thermal mockup.
#
# Version 2 provides better documentation and explanation, to match a new SOP

LOADCONFIG
HOME
SETLOG "LOG_Survey_1x2.txt"

PRINT "Survey_1x2_mockup running"

COPY $fiducial_dx 21.350
COPY $fiducial_dy 17.200
COPY $hdi_launch_pin_height 7.6132
COPY $ghco {0.849228,101.906706,-3.950}

COPY $top_pin_hdi_launch {170.013009,516.882913,65.153300}

# angles are in degrees

COPY $top_pin_chuck_0 {70.185163,516.689611,62.1817}
COPY $theta_chuck_0 0.222521
COPY $top_pin_chuck_1 {70.871527,364.151519,62.1817}
COPY $theta_chuck_1 0.405733
COPY $top_pin_chuck_2 {120.306212,364.591052,61.5567}
COPY $theta_chuck_2 -0.119763
COPY $top_pin_chuck_3 {170.377252,364.171766,61.9467}
COPY $theta_chuck_3 0.362119
COPY $top_pin_chuck_4 {219.931389,364.862527,61.8617}
COPY $theta_chuck_4 -0.121144

# bond pad centers on HDI launch pad
COPY $bond_pad_1 {161.637,527.670,73.137}
COPY $bond_pad_2 {161.900,548.903,73.127}
COPY $bond_pad_3 {161.804,549.504,73.132}
COPY $bond_pad_4 {162.066,570.729,73.067}
COPY $hdi_focus_z 66.193
COPY $heater_focus_z 66.463

COPY $tm_safe_z {0.000,0.000,-50.000}
COPY $tm_close_z {0.000,0.000,-5.000}

# this controls the position of the chip relative to the HDI
COPY $wire_bond_length 2.00
COPY $nominal_pad_1 {2.00,-0.15,0.00}
COPY $nominal_pad_4 {2.10,0.01,0.00}
COPY $fiducial_offset 1.41
COPY $pickup_offset 8.00
ADD $offset $pickup_offset $fiducial_offset
SUB $offset $offset $wire_bond_length
COPY $offset_dx {$offset,0.0,0.0}

SETVAC gantry_head_outer 0
SETVAC gantry_head_inner 0

@STEP_PROMPT GETINTPOPUP $station "Which assembly chuck? (0-4)"
GOTOIF @DISPLAY_ERROR `$station<0`
GOTOIF @STATION_0 `$station<1`
GOTOIF @STATION_1 `$station<2`
GOTOIF @STATION_2 `$station<3`
GOTOIF @STATION_3 `$station<4`
GOTOIF @STATION_4 `$station<5`
@DISPLAY_ERROR CHOICEPOPUP $trash "Bad number. Try again." "Sorry" "My bad"
GOTO @STEP_PROMPT
@STATION_0 COPY $theta_chuck $theta_chuck_0
COPY $top_pin_chuck $top_pin_chuck_0
GOTO @SURVEY_PART

@STATION_1 COPY $theta_chuck $theta_chuck_1
COPY $top_pin_chuck $top_pin_chuck_1
GOTO @SURVEY_PART

@STATION_2 COPY $theta_chuck $theta_chuck_2
COPY $top_pin_chuck $top_pin_chuck_2
GOTO @SURVEY_PART

@STATION_3 COPY $theta_chuck $theta_chuck_3
COPY $top_pin_chuck $top_pin_chuck_3
GOTO @SURVEY_PART

@STATION_4 COPY $theta_chuck $theta_chuck_4
COPY $top_pin_chuck $top_pin_chuck_4

PRINT "Surveying module on chuck %d" $station

@SURVEY_PART SUB $bond_pad_1 $bond_pad_1 $top_pin_hdi_launch
CALL @ROTATE $bond_pad_1 $theta_chuck -> $bond_pad_1
ADD $bond_pad_1 $bond_pad_1 $top_pin_chuck
COPY $bond_pad_1 {$bond_pad_1.x,$bond_pad_1.y,$hdi_focus_z}
SUB $bond_pad_1_safe $bond_pad_1 {0.0,0.0,20.0}
# PRINT "Should find bond pad 1 at %v mm" $bond_pad_1
MOVETO $bond_pad_1_safe 50
MOVETO $bond_pad_1 10

@STEP_PAD CHOICEPOPUP $choice "Survey center of HDI bond pad 1" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $survey_pad_1

SUB $heater_pad_1 $survey_pad_1 {$wire_bond_length,0.0,0.0}
COPY $heater_pad_1 {$heater_pad_1.x,$heater_pad_1.y,$heater_focus_z}
MOVETO $heater_pad_1 10

@STEP_PAD CHOICEPOPUP $choice "Survey center of heater bond pad 1" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $survey_other_1

SUB $delta_pad_1 $survey_pad_1 $survey_other_1
PRINT "Wire bond length (top) = %f mm (%f mm nominal)" $delta_pad_1.x $nominal_pad_1.x
PRINT "Slide = %f mm (%f mm nominal)" $delta_pad_1.y $nominal_pad_1.y

SUB $bond_pad_4 $bond_pad_4 $top_pin_hdi_launch
CALL @ROTATE $bond_pad_4 $theta_chuck -> $bond_pad_4
ADD $bond_pad_4 $bond_pad_4 $top_pin_chuck
COPY $bond_pad_4 {$bond_pad_4.x,$bond_pad_4.y,$hdi_focus_z}
# PRINT "Should find bond pad 4 at %v mm" $bond_pad_4
MOVETO $bond_pad_4 10

@STEP_PAD CHOICEPOPUP $choice "Survey center of HDI bond pad 4" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $survey_pad_4

SUB $heater_pad_4 $survey_pad_4 {$wire_bond_length,0.0,0.0}
COPY $heater_pad_4 {$heater_pad_4.x,$heater_pad_4.y,$heater_focus_z}
MOVETO $heater_pad_4 10

@STEP_PAD CHOICEPOPUP $choice "Survey center of heater bond pad 4" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $survey_other_4

SUB $delta_pad_4 $survey_pad_4 $survey_other_4
PRINT "Wire bond length (bottom) = %f mm (%f mm nominal)" $delta_pad_4.x $nominal_pad_4.x
PRINT "Slide = %f mm (%f mm nominal)" $delta_pad_4.y $nominal_pad_4.y

SUB $hdi_u $survey_pad_4 $survey_pad_1
SUB $heater_u $survey_other_4 $survey_other_1
ATAN2 $angle_hdi $hdi_u.y $hdi_u.x
ATAN2 $angle_heater $heater_u.y $heater_u.x
# PRINT "Angle of HDI bond pads = %f deg" $angle_hdi
# PRINT "Angle of heater bond pads = %f deg" $angle_heater
SUB $delta_angle $angle_heater $angle_hdi
PRINT "Angular deviation = %f deg" $delta_angle

CHOICEPOPUP $choice "Turn off vacuum?" "Yes, turn it off" "No, leave it on"
GOTOIFN @STEP_END $choice

SETVAC croc_chuck_{$station}_top 0
SETVAC croc_chuck_{$station}_bot 0

@STEP_END HOME
END

@STEP_ABORT GETPOS $current_pos
COPY $safe_z_pos {$current_pos.x,$current_pos.y,10.0}
MOVETO $safe_z_pos 5
SETVAC croc_weight_0 0
SETVAC croc_launchHDI 0
HOME
END

@ROTATE(vec,angle)
  SIN $sinth $angle
  COS $costh $angle
  MUL $dxx $vec.x $costh
  MUL $dxy $vec.y $sinth
  MUL $dyx $vec.x $sinth
  MUL $dyy $vec.y $costh
  COPY $vec {$dxx,$dyy,$vec.z}
  SUB $vec $vec {$dxy,0.0,0.0}
  ADD $vec $vec {0.0,$dyx,0.0}
  RETURN $vec
