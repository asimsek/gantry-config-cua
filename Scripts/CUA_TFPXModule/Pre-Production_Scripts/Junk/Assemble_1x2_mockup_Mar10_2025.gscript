# This script is to assemble a 1x2 thermal mockup.
#
# Version 2 provides better documentation and explanation, to match a new SOP

LOADCONFIG
HOME
SETLOG "LOG_Calibrate_Chuck.txt"

PRINT "Assemble_1x2_mockup running"

COPY $fiducial_dx 21.350
COPY $fiducial_dy 17.200
COPY $hdi_launch_pin_height 7.6132
COPY $ghco {0.849228,101.906706,-3.950}

COPY $top_pin_hdi_launch {170.013009,516.882913,65.153300}

# angles are in degrees

COPY $top_pin_chuck_0 {70.185163,516.689611,62.1817}
COPY $theta_chuck_0 0.222521
COPY $top_pin_chuck_1 {70.871527,364.151519,62.1817}
COPY $theta_chuck_1 0.405733
COPY $top_pin_chuck_2 {120.306212,364.591052,61.5567}
COPY $theta_chuck_2 -0.119763
COPY $top_pin_chuck_3 {170.377252,364.171766,61.9467}
COPY $theta_chuck_3 0.362119
COPY $top_pin_chuck_4 {219.931389,364.862527,61.8617}
COPY $theta_chuck_4 -0.121144

# bond pad centers on HDI launch pad
COPY $bond_pad_1 {161.637,527.670,73.137}
COPY $bond_pad_2 {161.900,548.903,73.127}
COPY $bond_pad_3 {161.804,549.504,73.132}
COPY $bond_pad_4 {162.066,570.729,73.067}

# fiducials on chip launch pad
COPY $tm_fid_1 {112.259,523.220,72.942}
COPY $tm_fid_2 {111.920,544.567,72.942}
COPY $tm_fid_3 {111.913,545.020,72.942}
COPY $tm_fid_4 {111.575,566.366,72.942}

COPY $tm_safe_z {0.000,0.000,-50.000}
COPY $tm_close_z {0.000,0.000,-5.000}

# this controls the position of the chip relative to the HDI
COPY $wire_bond_length 2.00
COPY $fiducial_offset 1.41
COPY $pickup_offset 8.00
ADD $offset $pickup_offset $fiducial_offset
SUB $offset $offset $wire_bond_length
COPY $offset_dx {$offset,0.130,0.0}

SETVAC gantry_head_outer 0
SETVAC gantry_head_inner 0
SETVAC croc_launchHDI 0
SETVAC croc_weight_0 0
SETVAC croc_launchROC_top 0
SETVAC croc_launchROC_bot 0

@STEP_HDI_LAUNCH SETVAC croc_launchHDI 0
CHOICEPOPUP $choice "Place HDI on the launch pad." "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

SETVAC croc_launchHDI 1
CHOICEPOPUP $choice "Place weight tool on HDI." "Yes, continue" "No, go back"
GOTOIFN @STEP_HDI_LAUNCH $choice

SETVAC croc_weight_0 1
WAIT 2000
SETVAC croc_launchHDI 0
WAIT 2000

MOVETO $top_pin_hdi_launch 100

CHOICEPOPUP $choice "Ready to survey bond pads?" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

@SURVEY_PAD1 MOVETO $bond_pad_1 10
CHOICEPOPUP $choice "Survey pad 1 bottom right corner." "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $pad_1_br
CHOICEPOPUP $choice "Survey pad 1 top left corner." "Yes, continue" "No, go back"
GOTOIFN @SURVEY_PAD1 $choice

VIDEO
GETPOS $pad_1_tl
ADD $pad_1 $pad_1_br $pad_1_tl
MUL $pad_1 $pad_1 {0.5,0.5,0.5}
SUB $pad_1_dx $pad_1 $bond_pad_1

@SURVEY_PAD2 ADD $bond_pad_2 $bond_pad_2 $pad_1_dx
MOVETO $bond_pad_2 10
CHOICEPOPUP $choice "Survey pad 2 bottom right corner." "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $pad_2_br
CHOICEPOPUP $choice "Survey pad 2 top left corner." "Yes, continue" "No, go back"
GOTOIFN @SURVEY_PAD2 $choice

VIDEO
GETPOS $pad_2_tl
ADD $pad_2 $pad_2_br $pad_2_tl
MUL $pad_2 $pad_2 {0.5,0.5,0.5}
SUB $pad_2_dx $pad_2 $bond_pad_2
ADD $pad_2_dx $pad_2_dx $pad_1_dx

@SURVEY_PAD3 ADD $bond_pad_3 $bond_pad_3 $pad_2_dx
MOVETO $bond_pad_3 10
CHOICEPOPUP $choice "Survey pad 3 bottom right corner." "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $pad_3_br
CHOICEPOPUP $choice "Survey pad 3 top left corner." "Yes, continue" "No, go back"
GOTOIFN @SURVEY_PAD3 $choice

VIDEO
GETPOS $pad_3_tl
ADD $pad_3 $pad_3_br $pad_3_tl
MUL $pad_3 $pad_3 {0.5,0.5,0.5}
SUB $pad_3_dx $pad_3 $bond_pad_3
ADD $pad_3_dx $pad_3_dx $pad_2_dx

@SURVEY_PAD4 ADD $bond_pad_4 $bond_pad_4 $pad_3_dx
ADD $bond_pad_4 $bond_pad_4 {$pad_2_dx.x,0.0,0.0}
MOVETO $bond_pad_4 10
CHOICEPOPUP $choice "Survey pad 4 bottom right corner." "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

VIDEO
GETPOS $pad_4_br
CHOICEPOPUP $choice "Survey pad 4 top left corner." "Yes, continue" "No, go back"
GOTOIFN @SURVEY_PAD4 $choice

VIDEO
GETPOS $pad_4_tl
ADD $pad_4 $pad_4_br $pad_4_tl
MUL $pad_4 $pad_4 {0.5,0.5,0.5}

PRINT "HDI pad 1 = %v mm" $pad_1
PRINT "HDI pad 2 = %v mm" $pad_2
PRINT "HDI pad 3 = %v mm" $pad_3
PRINT "HDI pad 4 = %v mm" $pad_4

# FIT $hdi_point $hdi_rot $hdi_residual TFPX_1X2_HDI $pad_1 $pad_2 $pad_3 $pad_4
# ATAN2 $hdi_theta $hdi_rot.x $hdi_rot.y
# PRINT "Fitted point on HDI = %v mm" $hdi_point
# PRINT "Rotation angle = %f deg" $hdi_theta
# PRINT "Residual = %f mm" $hdi_residual

COPY [0] $pad_1
ADD [1] $pad_2 {-0.100,0.0,0.0}
COPY [2] $pad_3
ADD [3] $pad_4 {-0.100,0.0,0.0}
FITLINE $hdi_rot $hdi_point $hdi_residual 0 4
ATAN2 $hdi_theta $hdi_rot.y $hdi_rot.x
PRINT "Fitted point on bond pad axis = %v mm" $hdi_point
PRINT "Unit vector on hdi = %v" $hdi_rot
PRINT "Rotation angle = %f deg" $hdi_theta
PRINT "Residual = %f mm" $hdi_residual

CHOICEPOPUP $choice "Place 1x2 thermal mockup on chip launchpad" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

SETVAC croc_launchROC_top 1
SETVAC croc_launchROC_bot 1

@SURVEY_FID1 CHOICEPOPUP $choice "Survey fiducial 1." "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice
MOVETO $tm_fid_1 20
VIDEO
GETPOS $survey_fid_1
SUB $fid_1_dx $survey_fid_1 $tm_fid_1

@SURVEY_FID2 CHOICEPOPUP $choice "Survey fiducial 2." "Yes, continue" "No, go back"
GOTOIFN @SURVEY_FID1 $choice
ADD $tm_fid_2 $tm_fid_2 $fid_1_dx
MOVETO $tm_fid_2 10
VIDEO
GETPOS $survey_fid_2
SUB $fid_2_dx $survey_fid_2 $tm_fid_2
ADD $fid_2_dx $fid_2_dx $fid_1_dx

@SURVEY_FID3 CHOICEPOPUP $choice "Survey fiducial 3." "Yes, continue" "No, go back"
GOTOIFN @SURVEY_FID2 $choice
ADD $tm_fid_3 $tm_fid_3 $fid_2_dx
MOVETO $tm_fid_3 10
VIDEO
GETPOS $survey_fid_3
SUB $fid_3_dx $survey_fid_3 $tm_fid_3
ADD $fid_3_dx $fid_3_dx $fid_2_dx

@SURVEY_FID4 CHOICEPOPUP $choice "Survey fiducial 4." "Yes, continue" "No, go back"
GOTOIFN @SURVEY_FID3 $choice
ADD $tm_fid_4 $tm_fid_4 $fid_3_dx
ADD $tm_fid_4 $tm_fid_4 {$fid_2_dx.x,0.0,0.0}
MOVETO $tm_fid_4 10
VIDEO
GETPOS $survey_fid_4

PRINT "Fiducial 1 = %v mm" $survey_fid_1
PRINT "Fiducial 2 = %v mm" $survey_fid_2
PRINT "Fiducial 3 = %v mm" $survey_fid_3
PRINT "Fiducial 4 = %v mm" $survey_fid_4

COPY [0] $survey_fid_1
COPY [1] $survey_fid_2
COPY [2] $survey_fid_3
COPY [3] $survey_fid_4
FITLINE $heater_rot $heater_point $heater_residual 0 4
ATAN2 $heater_theta $heater_rot.y $heater_rot.x
PRINT "Fitted point on fiducial axis = %v mm" $heater_point
PRINT "Unit vector on heater axis = %v" $heater_rot
PRINT "Rotation angle = %f deg" $heater_theta
PRINT "Residual = %f mm" $heater_residual

PRINT "HDI axis = %v" $hdi_rot
SUB $delta_theta $heater_theta $hdi_theta
CALL @ROTATE $heater_rot $delta_theta -> $new_heater_rot
PRINT "Rotated heater axis = %v" $new_heater_rot

# Calculate pick-up point on heater
COPY $pickup_dx {$heater_rot.y,$heater_rot.x,0.0}
MUL $pickup_dx $pickup_dx {1.0,-1.0,0.0}
MUL $pickup_dx $pickup_dx {$pickup_offset,$pickup_offset,0.0}
ADD $pickup_point $heater_point $pickup_dx
PRINT "Heater pickup point = %v mm" $pickup_point
ADD $pickup_point $pickup_point $ghco
ADD $safe_pickup_point $pickup_point {0.000,0.000,-2.000}
ADD $safer_pickup_point $pickup_point {0.000,0.000,-50.000}

CHOICEPOPUP $choice "Ready to pick up the part?" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

MOVENAME tool_rack_pos_2_out 200
LOADTOOL picker_tool
MOVETO $safer_pickup_point 100
MOVETO $safe_pickup_point 5
MOVETO $pickup_point 2
SETVAC gantry_head_inner 1
WAIT 2000
SETVAC croc_launchROC_top 0
SETVAC croc_launchROC_bot 0
WAIT 2000
MOVETO $safe_pickup_point 1
MOVETO $safer_pickup_point 20

@STEP_PROMPT GETINTPOPUP $station "Which assembly chuck? (0-4)"
GOTOIF @DISPLAY_ERROR `$station<0`
GOTOIF @STATION_0 `$station<1`
GOTOIF @STATION_1 `$station<2`
GOTOIF @STATION_2 `$station<3`
GOTOIF @STATION_3 `$station<4`
GOTOIF @STATION_4 `$station<5`
@DISPLAY_ERROR CHOICEPOPUP $trash "Bad number. Try again." "Sorry" "My bad"
GOTO @STEP_PROMPT

@STATION_0 COPY $theta_chuck $theta_chuck_0
COPY $top_pin_chuck $top_pin_chuck_0
GOTO @MOVE_PART

@STATION_1 COPY $theta_chuck $theta_chuck_1
COPY $top_pin_chuck $top_pin_chuck_1
GOTO @MOVE_PART

@STATION_2 COPY $theta_chuck $theta_chuck_2
COPY $top_pin_chuck $top_pin_chuck_2
GOTO @MOVE_PART

@STATION_3 COPY $theta_chuck $theta_chuck_3
COPY $top_pin_chuck $top_pin_chuck_3
GOTO @MOVE_PART

@STATION_4 COPY $theta_chuck $theta_chuck_4
COPY $top_pin_chuck $top_pin_chuck_4

@MOVE_PART SUB $hdi_dx $hdi_point $top_pin_hdi_launch
SIN $sinth $theta_chuck
COS $costh $theta_chuck
MUL $dxx $hdi_dx.x $costh
MUL $dxy $hdi_dx.y $sinth
MUL $dyx $hdi_dx.x $sinth
MUL $dyy $hdi_dx.y $costh
COPY $dx {$dxx,$dyy,0.0}
SUB $dx $dx {$dxy,0.0,0.0}
ADD $dx $dx {0.0,$dyx,0.0}
ADD $new_hdi_point $top_pin_chuck $dx
PRINT "New point on HDI = %v mm" $new_hdi_point

ADD $hdi_theta $hdi_theta $theta_chuck
SIN $sinth $hdi_theta
COS $costh $hdi_theta
MUL $dxx $offset_dx.x $costh
MUL $dxy $offset_dx.y $sinth
MUL $dyx $offset_dx.x $sinth
MUL $dyy $offset_dx.y $costh
COPY $dx {$dxx,$dyy,0.0}
SUB $dx $dx {$dxy,0.0,0.0}
ADD $dx $dx {0.0,$dyx,0.0}
ADD $drop_point $new_hdi_point $dx
COPY $drop_point {$drop_point.x,$drop_point.y,$heater_point.z}
# thickness of assembly carrier
SUB $drop_point $drop_point {0.0,0.0,6.400}
PRINT "Camera drop point = %v mm" $drop_point
ADD $drop_point $drop_point $ghco
PRINT "Gantry drop point = %v mm" $drop_point

SETVAC croc_chuck_{$station}_top 0
SETVAC croc_chuck_{$station}_bot 0

GETPOS $pos
COPY $pos.z 10
MOVETO $pos 5
SUB $safe_drop_point $drop_point {0.000,0.000,1.000}
COPY $safer_drop_point {$drop_point.x,$drop_point.y,$safer_pickup_point.z}
ADD $angle $delta_theta $theta_chuck
PRINT "Rotating heater by %f degrees" $angle
ROTATETO $angle 2
MOVETO $safer_drop_point 20
MOVETO $safe_drop_point 5
MOVETO $drop_point 2

SETVAC croc_chuck_{$station}_top 1
SETVAC croc_chuck_{$station}_bot 1
WAIT 2000
SETVAC gantry_head_inner 0
WAIT 2000
MOVETO $safe_drop_point 2
MOVETO $safer_drop_point 5
ROTATETO 0 5

MOVENAME tool_rack_pos_2_out 100
UNLOADTOOL picker_tool

HOME
END

@STEP_ABORT GETPOS $current_pos
COPY $safe_z_pos {$current_pos.x,$current_pos.y,10.0}
MOVETO $safe_z_pos 5
SETVAC croc_weight_0 0
SETVAC croc_launchHDI 0
HOME
END

@ROTATE(vec,angle)
  SIN $sinth $angle
  COS $costh $angle
  MUL $dxx $vec.x $costh
  MUL $dxy $vec.y $sinth
  MUL $dyx $vec.x $sinth
  MUL $dyy $vec.y $costh
  COPY $vec {$dxx,$dyy,$vec.z}
  SUB $vec $vec {$dxy,0.0,0.0}
  ADD $vec $vec {0.0,$dyx,0.0}
  RETURN $vec

