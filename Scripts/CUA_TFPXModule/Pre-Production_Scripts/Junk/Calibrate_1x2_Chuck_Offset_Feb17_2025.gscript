# This script is to measure displacements between HDI launch pad and assembly chucks
#
# Version 2 provides better documentation and explanation, to match a new SOP

LOADCONFIG
HOME
SETLOG "LOG_Calibrate_Chuck.txt"

COPY $fiducial_dx 21.350
COPY $fiducial_dy 17.200
COPY $hdi_launch_pin_height 7.6132

COPY $top_pin_hdi_launch {170.013009,516.882913,65.153300}
COPY $tm_fid_1 {159.154,527.765,72.9270}
COPY $tm_fid_2 {159.201,549.114,72.8910}
COPY $tm_fid_3 {159.201,549.569,72.9170}
COPY $tm_fid_4 {159.248,570.918,72.8970}

# angles are in degrees

COPY $top_pin_chuck_0 {70.185163,516.689611,62.1817}
COPY $theta_chuck_0 0.222521
COPY $top_pin_chuck_1 {70.871527,364.151519,62.1817}
COPY $theta_chuck_1 0.405733
COPY $top_pin_chuck_2 {120.306212,364.591052,61.5567}
COPY $theta_chuck_2 -0.119763
COPY $top_pin_chuck_3 {170.377252,364.171766,61.9467}
COPY $theta_chuck_3 0.362119
COPY $top_pin_chuck_4 {219.931389,364.862527,61.8617}
COPY $theta_chuck_4 -0.121144


COPY $chuck_offset_0 {-100.000,0.000,-6.335}
COPY $chuck_offset_1 {-100.000,-152.40,-6.335}
COPY $chuck_offset_2 {-50.000,-152.40,-6.335}
COPY $chuck_offset_3 {0.000,-152.40,-6.335}
COPY $chuck_offset_4 {50.000,-152.40,-6.335}

COPY $tm_safe_z {0.000,0.000,-50.000}
COPY $tm_close_z {0.000,0.000,-5.000}

ADD $tm_center $tm_fid_1 $tm_fid_2
ADD $tm_center $tm_center $tm_fid_3
ADD $tm_center $tm_center $tm_fid_4
MUL $tm_center $tm_center {0.25,0.25,0.25}
ADD $tm_center $tm_center $tm_safe_z

SETVAC gantry_head_outer 0
SETVAC gantry_head_inner 0
SETVAC croc_launchHDI 0
SETVAC croc_weight_0 0

MOVETO $top_pin_hdi_launch 100
@FIND_HDI_PIN CHOICEPOPUP $choice "Ready to survey top pin on HDI launch pad?" "Yes, continue" "No, skip"
GOTOIF @START_HDI_PIN $choice
COPY $center_pin_hdi_launch $top_pin_hdi_launch
GOTO @GOT_PIN

@START_HDI_PIN MOVETO $top_pin_hdi_launch 100
COPY $i 0
@GET_POINT_HDI_PIN VIDEO
GETPOS [$i]
ADD $i $i 1
CHOICEPOPUP $choice "Survey more points?" "Yes, continue." "No, fit circle."
GOTOIF @GET_POINT_HDI_PIN $choice
FITCIRCLE $center_pin_hdi_launch $radius $residual 0 $i

CHOICEPOPUP $choice "Satisfactory fit?" "Yes, continue." "No, try again."
GOTOIFN @START_HDI_PIN $choice

@GOT_PIN PRINT "HDI launch pad top pin = %v mm" $center_pin_hdi_launch
SUB $delta_pin $center_pin_hdi_launch $top_pin_hdi_launch
PRINT "Change in surveyed pin location = %v mm" $delta_pin

@STEP_ONE CHOICEPOPUP $choice "Place 1x2 thermal mockup and weight tool on HDI launchpad" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice

SETVAC croc_weight_0 1
WAIT 2000
SETVAC croc_launchHDI 0
WAIT 2000

@STEP_FID1 CHOICEPOPUP $choice "Survey fiducial 1" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice
MOVETO $tm_fid_1 10
VIDEO
GETPOS $survey_fid_1
PRINT "Survey fid 1 = %v" $survey_fid_1
SUB $delta_1 $survey_fid_1 $tm_fid_1
COPY $delta $delta_1

@STEP_FID2 CHOICEPOPUP $choice "Survey fiducial 2" "Yes, continue" "No, go back"
GOTOIFN @STEP_FID1 $choice
ADD $tm_fid_2 $tm_fid_2 $delta
MOVETO $tm_fid_2 10
VIDEO
GETPOS $survey_fid_2
PRINT "Survey fid 2 = %v" $survey_fid_2
SUB $delta_2 $survey_fid_2 $tm_fid_2
ADD $delta $delta $delta_2

@STEP_FID3 CHOICEPOPUP $choice "Survey fiducial 3" "Yes, continue" "No, go back"
GOTOIFN @STEP_FID2 $choice
ADD $tm_fid_3 $tm_fid_3 $delta
MOVETO $tm_fid_3 10
VIDEO
GETPOS $survey_fid_3
PRINT "Survey fid 3 = %v" $survey_fid_3
SUB $delta_3 $survey_fid_3 $tm_fid_3
ADD $delta $delta $delta_3

@STEP_FID4 CHOICEPOPUP $choice "Survey fiducial 4" "Yes, continue" "No, go back"
GOTOIFN @STEP_FID3 $choice
ADD $tm_fid_4 $tm_fid_4 $delta
MOVETO $tm_fid_4 10
VIDEO
GETPOS $survey_fid_4
PRINT "Survey fid 4 = %v" $survey_fid_4

@STEP_TOOL CHOICEPOPUP $choice "Ready to move the part?" "Yes, continue" "No, go back"
GOTOIFN @STEP_FID4 $choice

COPY [0] $survey_fid_1
COPY [1] $survey_fid_2
COPY [2] $survey_fid_3
COPY [3] $survey_fid_4
FITLINE $axis_hdi_launch $point_hdi_launch $residual 0 4

GETPOS $out_of_the_way
COPY $out_of_the_way {$out_of_the_way.x,$out_of_the_way.y,10.0}
MOVETO $out_of_the_way 10

@STEP_PROMPT GETINTPOPUP $station "Which assembly chuck? (0-4)"
GOTOIF @DISPLAY_ERROR `$station<0`
GOTOIF @STATION_0 `$station<1`
GOTOIF @STATION_1 `$station<2`
GOTOIF @STATION_2 `$station<3`
GOTOIF @STATION_3 `$station<4`
GOTOIF @STATION_4 `$station<5`
@DISPLAY_ERROR CHOICEPOPUP $trash "Bad number. Try again." "Sorry" "My bad"
GOTO @STEP_PROMPT

@STATION_0 ADD $tm_center $tm_center $chuck_offset_0
ADD $tm_fid_1 $survey_fid_1 $chuck_offset_0
ADD $tm_fid_2 $survey_fid_2 $chuck_offset_0
ADD $tm_fid_3 $survey_fid_3 $chuck_offset_0
ADD $tm_fid_4 $survey_fid_4 $chuck_offset_0
COPY $top_pin $top_pin_chuck_0
GOTO @SURVEY_AGAIN
@STATION_1 ADD $tm_center $tm_center $chuck_offset_1
ADD $tm_fid_1 $survey_fid_1 $chuck_offset_1
ADD $tm_fid_2 $survey_fid_2 $chuck_offset_1
ADD $tm_fid_3 $survey_fid_3 $chuck_offset_1
ADD $tm_fid_4 $survey_fid_4 $chuck_offset_1
COPY $top_pin $top_pin_chuck_1
GOTO @SURVEY_AGAIN
@STATION_2 ADD $tm_center $tm_center $chuck_offset_2
ADD $tm_fid_1 $survey_fid_1 $chuck_offset_2
ADD $tm_fid_2 $survey_fid_2 $chuck_offset_2
ADD $tm_fid_3 $survey_fid_3 $chuck_offset_2
ADD $tm_fid_4 $survey_fid_4 $chuck_offset_2
COPY $top_pin $top_pin_chuck_2
GOTO @SURVEY_AGAIN
@STATION_3 ADD $tm_center $tm_center $chuck_offset_3
ADD $tm_fid_1 $survey_fid_1 $chuck_offset_3
ADD $tm_fid_2 $survey_fid_2 $chuck_offset_3
ADD $tm_fid_3 $survey_fid_3 $chuck_offset_3
ADD $tm_fid_4 $survey_fid_4 $chuck_offset_3
COPY $top_pin $top_pin_chuck_3
GOTO @SURVEY_AGAIN
@STATION_4 ADD $tm_center $tm_center $chuck_offset_4
ADD $tm_fid_1 $survey_fid_1 $chuck_offset_4
ADD $tm_fid_2 $survey_fid_2 $chuck_offset_4
ADD $tm_fid_3 $survey_fid_3 $chuck_offset_4
ADD $tm_fid_4 $survey_fid_4 $chuck_offset_4
COPY $top_pin $top_pin_chuck_4
GOTO @SURVEY_AGAIN

@SURVEY_AGAIN MOVETO $top_pin 50
CHOICEPOPUP $choice "Survey top pin?" "Yes, continue." "No, skip."
GOTOIF @SURVEY_PIN $choice
COPY $center_pin_chuck $top_pin
GOTO @SURVEY_FID1

@SURVEY_PIN COPY $i 0
@GET_POINT_PIN VIDEO
GETPOS [$i]
ADD $i $i 1
CHOICEPOPUP $choice "Survey more points?" "Yes, continue." "No, fit circle."
GOTOIF @GET_POINT_PIN $choice
FITCIRCLE $center_pin_chuck $radius $residual 0 $i

CHOICEPOPUP $choice "Satisfactory fit?" "Yes, continue." "No, try again."
GOTOIFN @SURVEY_AGAIN $choice

PRINT "Chuck %d top pin = %v" $station $center_pin_chuck

@SURVEY_FID1 CHOICEPOPUP $choice "Survey fiducial 1" "Yes, continue" "No, abort"
GOTOIFN @STEP_ABORT $choice
MOVETO $tm_fid_1 10
VIDEO
GETPOS $new_fid_1
PRINT "New fid 1 = %v" $new_fid_1

SUB $delta_1 $new_fid_1 $tm_fid_1
COPY $delta $delta_1

@SURVEY_FID2 CHOICEPOPUP $choice "Survey fiducial 2" "Yes, continue" "No, go back"
GOTOIFN @SURVEY_FID1 $choice
ADD $tm_fid_2 $tm_fid_2 $delta
MOVETO $tm_fid_2 10
VIDEO
GETPOS $new_fid_2
PRINT "New fid 2 = %v" $new_fid_2

SUB $delta_2 $new_fid_2 $tm_fid_2
ADD $delta $delta $delta_2

@SURVEY_FID3 CHOICEPOPUP $choice "Survey fiducial 3" "Yes, continue" "No, go back"
GOTOIFN @SURVEY_FID2 $choice
ADD $tm_fid_3 $tm_fid_3 $delta
MOVETO $tm_fid_3 10
VIDEO
GETPOS $new_fid_3
PRINT "New fid 3 = %v" $new_fid_3

SUB $delta_3 $new_fid_3 $tm_fid_3
ADD $delta $delta $delta_3

@SURVEY_FID4 CHOICEPOPUP $choice "Survey fiducial 4" "Yes, continue" "No, go back"
GOTOIFN @SURVEY_FID3 $choice
ADD $tm_fid_4 $tm_fid_4 $delta
MOVETO $tm_fid_4 10
VIDEO
GETPOS $new_fid_4
PRINT "New fid 4 = %v" $new_fid_4

COPY [0] $new_fid_1
COPY [1] $new_fid_2
COPY [2] $new_fid_3
COPY [3] $new_fid_4
FITLINE $axis_chuck $point_chuck $residual 0 4

ATAN2 $theta_hdi_launch $axis_hdi_launch.x $axis_hdi_launch.y
ATAN2 $theta_chuck $axis_chuck.x $axis_chuck.y
SUB $theta $theta_chuck $theta_hdi_launch
PRINT "Difference in angle = %f deg" $theta
SIN $sinth $theta
COS $costh $theta

SUB $dx $point_hdi_launch $center_pin_hdi_launch
# PRINT "Delta x on launch pad = %v mm" $dx
MUL $dxx $dx.x $costh
MUL $dxy $dx.y $sinth
MUL $dyx $dx.x $sinth
MUL $dyy $dx.y $costh
SUB $dx.x $dxx $dxy
ADD $dx.y $dyy $dyx
# PRINT "Rotated Delta x = %v mm" $dx
SUB $new_chuck_pin_center $point_chuck $dx
SUB $delta_pin $new_chuck_pin_center $center_pin_chuck
PRINT "New pin location on chuck %d = %v mm" $station $new_chuck_pin_center
PRINT "Deviation = %v mm" $delta_pin

HOME
END

@STEP_ABORT GETPOS $current_pos
COPY $safe_z_pos {$current_pos.x,$current_pos.y,10.0}
MOVETO $safe_z_pos 2
SETVAC croc_weight_0 0
SETVAC croc_launchHDI 0
HOME
END
